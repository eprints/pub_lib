# panels definition for use on the eprints summary page
$c->{eprint_summary_panels_local} =
{
  # just show the eprint abstract
  abstract =>
  {
    citation => "panel_simple",
    title => "Abstract",
    params =>
    {
    },
    data =>
    {
      metadata => [ qw/
abstract
      / ],
    }
  },
  # show the main eprint metadata fields
  metadata =>
  {
    citation => "panel_table",
    title => "Information",
    params =>
    {
    },
    data =>
    {
      metadata => [ qw/
title
creators_name
editors_name
contributors
id_number
official_url
date
subjects
divisions
commentary
note
keywords
      / ],
    }
  },
  # fields more of interest to the library
  library =>
  {
    citation => "panel_table",
    title => "Library",
    params =>
    {
    },
    data =>
    {
      metadata => [ qw/
type
pres_type
monograph_type
thesis_type
userid
sword_depositor
datestamp
rev_number
lastmod
      / ],
    }
  },
};

# utility routine to render a link
$c->{eprint_render_link} = sub
{
  my( $repository, $url, $text ) = @_;

  my $link = $repository->make_element( "a", href => $url );
  $link->appendChild( $repository->make_text( $text ) );

  return $link;
};

# pass the panels config to the render function
$c->{eprint_render_panels_local} = sub
{
  my( $eprint, $repository ) = @_;

  my @panels_to_show = qw/ abstract metadata library /;

  # optional list of additional data keys and their values, in this case URI on the metadata panel
  push @{ $c->{eprint_summary_panels_local}->{metadata}->{data}->{keys} }, "URI";
  $c->{eprint_summary_panels_local}->{metadata}->{data}->{URI} = &{$c->{eprint_render_link}}( $repository, $eprint->uri(), $eprint->uri() );

  # call and return the main panel render code
  return &{$c->{render_panels}}( $eprint, $repository, $c->{eprint_summary_panels_local}, \@panels_to_show );
};

$c->{summary_page_metadata} = [qw/
/];

# a version of the classic eprint render routine, with panels rendered as tabs to show the abstract and metadata fields
$c->{eprint_render} = sub
{
	my( $eprint, $repository, $preview ) = @_;
	my $succeeds_field = $repository->dataset( "eprint" )->field( "succeeds" );
	my $commentary_field = $repository->dataset( "eprint" )->field( "commentary" );

	my $flags = { 
		has_multiple_versions => $eprint->in_thread( $succeeds_field ),
		in_commentary_thread => $eprint->in_thread( $commentary_field ),
		preview => $preview,
	};
	my %fragments = ();

	# Put in a message describing how this document has other versions
	# in the repository if appropriate
	if( $flags->{has_multiple_versions} )
	{
		my $latest = $eprint->last_in_thread( $succeeds_field );
		if( $latest->value( "eprintid" ) == $eprint->value( "eprintid" ) )
		{
			$flags->{latest_version} = 1;
			$fragments{multi_info} = $repository->html_phrase( "page:latest_version" );
		}
		else
		{
			$fragments{multi_info} = $repository->render_message(
				"warning",
				$repository->html_phrase( 
					"page:not_latest_version",
					link => $repository->render_link( $latest->get_url() ) ) );
		}
	}		


	# Now show the version and commentary response threads
	if( $flags->{has_multiple_versions} )
	{
		$fragments{version_tree} = $eprint->render_version_thread( $succeeds_field );
	}
	
	if( $flags->{in_commentary_thread} )
	{
		$fragments{commentary_tree} = $eprint->render_version_thread( $commentary_field );
	}

	foreach my $key ( keys %fragments ) { $fragments{$key} = [ $fragments{$key}, "XHTML" ]; }
	
	my $page = $eprint->render_citation( "summary_page1", %fragments, flags=>$flags );

	my $panelfn = $c->{eprint_render_panels_local};
	$page->appendChild( &{$panelfn}( $eprint, $repository ) );

	$page->appendChild( $eprint->render_citation( "summary_page2", %fragments, flags=>$flags ) );

	my $title = $eprint->render_citation("brief");

	my $links = $repository->xml()->create_document_fragment();
	if( !$preview )
	{
		$links->appendChild( $repository->plugin( "Export::Simple" )->dataobj_to_html_header( $eprint ) );
		$links->appendChild( $repository->plugin( "Export::DC" )->dataobj_to_html_header( $eprint ) );
	}

	return( $page, $title, $links );
};

=head1 COPYRIGHT

=for COPYRIGHT BEGIN

Copyright 2016 University of Southampton.
EPrints 3.4 preview 2 is supplied by EPrints Services.
This software is supplied as is and is for demonstration purposes.
This software may be used with permission and must not be redistributed.
http://www.eprints.org/eprints-3.4/

=for COPYRIGHT END

=for LICENSE BEGIN

This file is part of EPrints 3.4 L<http://www.eprints.org/>.

EPrints 3.4 and this file are released under the terms of the
GNU Lesser General Public License version 3 as published by
the Free Software Foundation unless otherwise stated.

EPrints 3.4 is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
See the GNU Lesser General Public License for more details.

You should have received a copy of the GNU Lesser General Public
License along with EPrints 3.4.
If not, see L<http://www.gnu.org/licenses/>.

=for LICENSE END

